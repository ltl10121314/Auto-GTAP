!<
    =============================
    Global Trade Analysis Project
    The GTAP Modeling Framework  
    GTAPVIEW.TAB FILE            
    =============================

    Authored by Thomas Hertel, Melanie Bacou,and Wusheng Yu

    --------------------
    History of this file
    --------------------
    April 29, 1998: Following features are new:
        - disposition of output across all uses
        - full set of tax rates are available now for all transactions
    Sept, 1998: eliminate unnecessary sets.
    October, 1998: add code to compute the initial tax rates.
    May, 2000: Modified by Ken Itakura
        - added Appendix on elasticities
        - reformatted 
    Dec, 2002: Added data checking outputs at the end. 
        - set elements "exp" and "prod" are now TABLO keywords
           replaced them with "export" and "production"
    --------------------------------------
    Overview of the GTAPVIEW.TAB structure
    --------------------------------------
    FILE declaration
    SET declaration
    READ statements of Base Data
    Derivatives of the Base Data
        1. Macro variables ( Reg )
        2.Trade, Transport and Protection ( Reg x Reg )
        3. Commodities ( Comm x Reg )
        4. Sectors ( Comm x Reg )
    Write statements of the Derivatives
    Computation of Initial Tax Rates
    Appendix: Derived Elasticities of demands

>!

!<
    -----
    FILES
    -----
>!

File
    GTAPDATA # File containing all base data #;
File
    GTAPSETS # File with set specification #;
File
    GTAPPARM # File containing behavioral prameters #;
   ! above not actually used, but must leave in !
File (new)
    GTAPVIEW # File with headers for viewing #;
File (new)
    TAXRATES # File with headers for viewing initial tax rates #;

!<
    ----
    SETS
    ----
>!

Set
    REG # Regions in the model #
    maximum size 10 read elements from file GTAPSETS header "H1";
Set
    TRAD_COMM # Traded commodities #
    maximum size 10 read elements from file GTAPSETS header "H2";
Set
    MARG_COMM # Margin commodities #
    maximum size 10 read elements from file GTAPSETS header "MARG";
Subset
    MARG_COMM is subset of TRAD_COMM;
Set
    NMRG_COMM # Non-margin commodities # = TRAD_COMM - MARG_COMM;
Set
    CGDS_COMM # Capital goods commodities #
    maximum size 1  read elements from file GTAPSETS header "H9";
Set
    ENDW_COMM # Endowment commodities #
    maximum size 5  read elements from file GTAPSETS header "H6";
Set
    TARSET # Components of tariff #   
    read elements from file GTAPSETS header "TARS";

Set
    ENDWC_COMM # Capital Endowment Commodity # (capital);
Set
    PROD_COMM # PRODUCED COMMODITIES  # = TRAD_COMM union CGDS_COMM;
Set
    DEMD_COMM # DEMANDED COMMODITIES # = ENDW_COMM union TRAD_COMM;
Set
    NSAV_COMM # NON-SAVINGS COMMODITIES # = DEMD_COMM union CGDS_COMM;
Subset
    PROD_COMM is subset of NSAV_COMM;
Subset
    ENDWC_COMM is subset of NSAV_COMM;

!<
    The following are new sets for GTAPVIEW
    Supplementary sets for "toggles" in VIEWHAR
>!

Set GDPEXPEND(cons,inv,gov,export,imp);
Set GDPSOURCE(fact,tax,depr);
Set CAPACCT(save,inv);
Set CURACCT(export,imp);
Set MKTVALUE(fob,trans);
Set OUT(dom,trans,export);
Set SALES(production,cons,gov);
Set PURCHVALUE(mktexp,comtax);
Set DIR(domestic,imported);
Set OUTVALUE(prodrev,outtax);
Set EXPVALUE(exprev,xtax);
Set IMPVALUE(impcost,mtax);

!<
    ----------------------------
    READ statements of Base Data
    ----------------------------

    base revenues and expenditures at agent's prices
    ------------------------------------------------
>!

Coefficient (all,i,ENDW_COMM)(all,r,REG)
    EVOA(i,r) # Value of output at agent's prices #;
Read
    EVOA from file GTAPDATA header "EVOA";

Coefficient (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    EVFA(i,j,r) # Producer expenditure on i by industry j #;
Read
    EVFA from file GTAPDATA header "EVFA";

Coefficient (all,r,REG)
    save(r) # Expenditure on NET savings in region r valued at agent's prices #;
Read
    save from file GTAPDATA header "SAVE";

Coefficient (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VIFA(i,j,r) # Purchases of imported i r for use in j in region r #;
Read
    VIFA from file GTAPDATA header "VIFA";

Coefficient (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VDFA(i,j,r) # Purchases of domestic i r for use in j in region r #;
Read
    VDFA from file GTAPDATA header "VDFA";

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VIPA(i,r) # Private household expenditure on imported i in r #;
Read
    VIPA from file GTAPDATA header "VIPA";

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VDPA(i,r) # Private household expenditure on domestic i in r #;
Read
    VDPA from file GTAPDATA header "VDPA";

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VIGA(i,r) # Government household expenditure on imported i #;
Read
    VIGA from file GTAPDATA header "VIGA";

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VDGA(i,r) # Government household expenditure on domestic i in r #;
Read
    VDGA from file GTAPDATA header "VDGA";

Coefficient (all, r, REG)
    VKB(r) # Value of beginning-of-period capital stock, in region r #;
Read
    VKB from file GTAPDATA header "VKB";

Coefficient (all, r, REG)
    VDEP(r) # Value of capital depreciation in r (exogenous) #;
Read
    VDEP from file GTAPDATA header "VDEP";

!<
    base revenues and expenditures at prices
    ----------------------------------------
>!

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VXMD(i,r,s) # Exports of i from r to s at market prices (tradeables only) #;
Read
    VXMD from file GTAPDATA header "VXMD";

Coefficient (ge 0)(all,m,MARG_COMM)(all,r,REG)
    VST(m,r)
    # Exports of m from r for int'l transport at mkt prices (tradeables only) #;
Read
    VST from file GTAPDATA header "VST";

Coefficient (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    VFM(i,j,r) # Producer expenditure on i by j in r at market prices #;
Read
    VFM from file GTAPDATA header "VFM";

Coefficient (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VIFM(i,j,r) # Purchases of imports i for use in j in region r #;
Read
    VIFM from file GTAPDATA header "VIFM";

Coefficient (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VDFM(i,j,r) # Purchases of domestic i r for use in j in region r #;
Read
    VDFM from file GTAPDATA header "VDFM";

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VIPM(i,r) # Private household expenditure on i in r #;
Read
    VIPM from file GTAPDATA header "VIPM";

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VDPM(i,r) # Private household expenditure on domestic i in r #;
Read
    VDPM from file GTAPDATA header "VDPM";

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VIGM(i,r) # Gov't household expenditure on i in r #;
Read
    VIGM from file GTAPDATA header "VIGM";

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VDGM(i,r) # Government household expenditure on domestic i in r #;
Read
    VDGM from file GTAPDATA header "VDGM";

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VIMS(i,r,s) # Imports of i from r to s valued at domestic mkt prices #;
Read
    VIMS from file GTAPDATA header "VIMS";


!<
    base revenues and expenditures at world prices
    ----------------------------------------------
>!

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VXWD(i,r,s) # Exports of i from r to s valued fob (tradeables only) #;
Read
    VXWD from file GTAPDATA header "VXWD";

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VIWS(i,r,s) # Imports of i from r to s valued cif (tradeables only) #;
Read
    VIWS from file GTAPDATA header "VIWS";

Coefficient (all,k,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VTWR(k,i,r,s)
    # Value of transport services at world prices, by destination #;
Read
    VTWR from file GTAPDATA header "VTWR";

!<
    ----------------------------
    DERIVATIVES OF THE BASE DATA
    ----------------------------

    1. Macro variables ( Reg )
    --------------------------
        GDPEXP
        GDPSRC
        CAPITALACCT
        CURRENTACCT

>!

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VPA(i,r) # Private household expenditure on i in r at agent's prices #;
Formula (all,i,TRAD_COMM)(all,s,REG)
    VPA(i,s) = VDPA(i,s) + VIPA(i,s);
Coefficient (all,r,REG)
    PRIVEXP(r) # Private consumption expenditure in region r #;
Formula (all,r,REG)
    PRIVEXP(r) = sum{i,TRAD_COMM, VPA(i,r)};

Coefficient (all,i,DEMD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VFA(i,j,r) # Firm's expenditure on i by industry j #;
Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    VFA(i,j,r) = EVFA(i,j,r);
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    VFA(i,j,s) = VDFA(i,j,s) + VIFA(i,j,s);
Coefficient (all,i,NSAV_COMM)(all,r,REG)
    VOA(i,r) # Value of commodity i output in region r #;
Formula (all,i,ENDW_COMM)(all,r,REG)
    VOA(i,r) = EVOA(i,r);
Formula (all,i,PROD_COMM)(all,r,REG)
    VOA(i,r) = sum{j,DEMD_COMM, VFA(j,i,r)};
Coefficient (all, r, REG)
    REGINV(r) # Regional gross investment in region r #;
Formula (all, r, REG)
    REGINV(r) = sum{k,CGDS_COMM, VOA(k,r)};

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VGA(i,r) # Government household expenditure on i in r at agent's prices #;
Formula (all,i,TRAD_COMM)(all,s,REG)
    VGA(i,s) = VDGA(i,s) + VIGA(i,s);
Coefficient (all,r,REG)
    GOVEXP(r) # Government expenditure in region r #;
Formula (all,r,REG)
    GOVEXP(r) = sum{i,TRAD_COMM, VGA(i,r)};

Coefficient (all,r,REG)
    VEXPORTS(r)
    # Value of merchandise and services sales to the world market from r #;
Formula (all,r,REG)
    VEXPORTS(r)
        = sum{i,NMRG_COMM, sum{s,REG, VXWD(i,r,s)}}
        + sum{m,MARG_COMM, sum{s,REG, VXWD(m,r,s)} + VST(m,r)}
;

Coefficient (all,s,REG)
    VIMPORTS(s)
    # Value of merchandise and services purchases from world market by s #;
Formula (all,s,REG)
    VIMPORTS(s) = sum{i,TRAD_COMM, sum{r,REG, VIWS(i,r,s)}};

Coefficient  (all,r,REG)(all,k,GDPEXPEND)
    GDPEXP(r,k) # Composition of GDP from the expenditure side #;
Formula  (all,r,REG)
    GDPEXP(r,"cons") = PRIVEXP(r);
Formula  (all,r,REG)
    GDPEXP(r,"inv") = REGINV(r);
Formula  (all,r,REG)
    GDPEXP(r,"gov") = GOVEXP(r);
Formula  (all,r,REG)
    GDPEXP(r,"export") = VEXPORTS(r);
Formula  (all,r,REG)
    GDPEXP(r,"imp") = - VIMPORTS(r);

Coefficient (all,r,REG)
    NETFACTINC(r) # Factor income, net of depreciation #;
Formula (all,r,REG)
    NETFACTINC(r) = sum{i,ENDW_COMM, VOA(i,r)} - VDEP(r);

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VDM(i,r) # Domestic sales of i in r at market prices (tradeables only) #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    VDM(i,r) = VDPM(i,r) + VDGM(i,r) + sum{j,PROD_COMM, VDFM(i,j,r)};

Coefficient (all,i,NSAV_COMM)(all,r,REG)
    VOM(i,r) # Value of commodity i output in region r at market prices #;
Formula (all,i,ENDW_COMM)(all,r,REG)
    VOM(i,r) = sum{j,PROD_COMM, VFM(i,j,r)};
Formula (all,m,MARG_COMM)(all,r,REG)
    VOM(m,r) = VDM(m,r) + sum{s,REG, VXMD(m,r,s)} + VST(m,r);
Formula (all,i,NMRG_COMM)(all,r,REG)
    VOM(i,r) = VDM(i,r) + sum{s,REG, VXMD(i,r,s)};
Formula (all,h,CGDS_COMM)(all,r,REG)
    VOM(h,r) = VOA(h,r);

Coefficient (all,r,REG)
    NETAXES(r) # Net tax revenues in r #;
Formula (all,r,REG)
    NETAXES(r)
        = sum{i,NSAV_COMM, VOM(i,r) - VOA(i,r)}
        + sum{j,PROD_COMM, sum{i,ENDW_COMM, VFA(i,j,r) - VFM(i,j,r)}}
        + sum{i,TRAD_COMM, VIPA(i,r) - VIPM(i,r)}
        + sum{i,TRAD_COMM, VDPA(i,r) - VDPM(i,r)}
        + sum{i,TRAD_COMM, VIGA(i,r) - VIGM(i,r)}
        + sum{i,TRAD_COMM, VDGA(i,r) - VDGM(i,r)}
        + sum{j,PROD_COMM, sum{i,TRAD_COMM, VIFA(i,j,r) - VIFM(i,j,r)}}
        + sum{j,PROD_COMM, sum{i,TRAD_COMM, VDFA(i,j,r) - VDFM(i,j,r)}}
        + sum{i,TRAD_COMM, sum{s,REG, VXWD(i,r,s) - VXMD(i,r,s)}}
        + sum{i,TRAD_COMM, sum{s,REG, VIMS(i,s,r) - VIWS(i,s,r)}}
;

Coefficient (all,r,REG)(all,k,GDPSOURCE)
    GDPSRC(r,k) # Composition of GDP from the sources side #;
Formula (all,r,REG)
    GDPSRC(r,"fact") = NETFACTINC(r);
Formula (all,r,REG)
    GDPSRC(r,"tax") = NETAXES(r);
Formula (all,r,REG)
    GDPSRC(r,"depr") = VDEP(r);

Coefficient (all, r, REG)
    NETINV(r) # Regional net investment in region r #;
Formula (all, r, REG)
    NETINV(r) = REGINV(r) - VDEP(r);

Coefficient (all,r,REG)(all,k,CAPACCT)
    CAPITALACCT(r,k) # S - I = X - M #;
Formula (all,r,REG)
    CAPITALACCT(r,"save") = save(r);
Formula (all,r,REG)
    CAPITALACCT(r,"inv") = - NETINV(r);

Coefficient (all,r,REG)(all,k,CURACCT)(all,i,TRAD_COMM)
    CURRENTACCT(r,k,i) # X - M = S - I #;
Formula (all,r,REG)(all,m,MARG_COMM)
    CURRENTACCT(r,"export",m) = sum{s,REG, VXWD(m,r,s)} + VST(m,r);
Formula (all,r,REG)(all,i,NMRG_COMM)
    CURRENTACCT(r,"export",i) = sum{s,REG, VXWD(i,r,s)};
Formula  (all,r,REG)(all,i,TRAD_COMM)
    CURRENTACCT(r,"imp",i) = - sum{s,REG, VIWS(i,s,r)};

!<
    2.Trade, Transport and Protection ( Reg x Reg )
    -----------------------------------------------
        VALEXPORTS
        VALIMPORTS
        CIFDECOMP

>!

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)(all,k,EXPVALUE)
    VALEXPORTS(i,r,s,k) # Value of exports at world prices #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VALEXPORTS(i,r,s,"exprev") = VXMD(i,r,s);
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VALEXPORTS(i,r,s,"xtax") = VXWD(i,r,s) - VXMD(i,r,s);

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)(all,k,IMPVALUE)
    VALIMPORTS(i,r,s,k) # Value of imports at domestic prices #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VALIMPORTS(i,r,s,"impcost") = VIWS(i,r,s);
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VALIMPORTS(i,r,s,"mtax") = VIMS(i,r,s) - VIWS(i,r,s);

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)(all,j,MKTVALUE)
    CIFDECOMP(i,r,s,j) # Value of tradables from r to s #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    CIFDECOMP(i,r,s,"fob") = VXWD(i,r,s);
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    CIFDECOMP(i,r,s,"trans") = VIWS(i,r,s) - VXWD(i,r,s);

!<
    3. Commodities ( Comm x Reg )
    -----------------------------
        OUTDISP
        DOMSALESDISP
        IMPSALESDISP
        VALOUTPUT
        SUFFICIENCY

>!

Coefficient (all,i,TRAD_COMM)(all,j,OUT)(all,r,REG)
    OUTDISP(i,j,r)
    # Value of sales of tradables to different markets at dom market price #;
Formula (all,i,TRAD_COMM)(all,j,OUT)(all,r,REG)
    OUTDISP(i,j,r) = 0.0;
Formula (all,i,TRAD_COMM)(all,r,REG)
    OUTDISP(i,"dom",r) = VDM(i,r);
Formula (all,m,MARG_COMM)(all,r,REG)
    OUTDISP(m,"trans",r) = VST(m,r);
Formula (all,i,TRAD_COMM)(all,r,REG)
    OUTDISP(i,"export",r) = sum{s,REG, VXMD(i,r,s)};

Coefficient (all,i,TRAD_COMM)(all,m,SALES)(all,r,REG)
    DOMSALESDISP(i,m,r)
    # Value of domestic sales of tradables at dom market price #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    DOMSALESDISP(i,"production",r) = sum{j,PROD_COMM, VDFM(i,j,r)};
Formula  (all,i,TRAD_COMM)(all,r,REG)
    DOMSALESDISP(i,"cons",r) = VDPM(i,r);
Formula  (all,i,TRAD_COMM)(all,r,REG)
    DOMSALESDISP(i,"gov",r) = VDGM(i,r);

Coefficient (all,i,TRAD_COMM)(all,m,SALES)(all,r,REG)
    IMPSALESDISP(i,m,r)
    # Value of sales of tradables to different markets at dom market price #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    IMPSALESDISP(i,"production",r) = sum{j,PROD_COMM, VIFM(i,j,r)};
Formula (all,i,TRAD_COMM)(all,r,REG)
    IMPSALESDISP(i,"cons",r) = VIPM(i,r);
Formula (all,i,TRAD_COMM)(all,r,REG)
    IMPSALESDISP(i,"gov",r) = VIGM(i,r);

!<
    Finally, calculate some policy instruments of interest.
    Note that this approach produces positive taxes,
    but GROSS tax rates.
>!
Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,k,OUTVALUE)
    VALOUTPUT(i,r,k) # Value of output at agent's prices #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    VALOUTPUT(i,r,"prodrev") = VOA(i,r);
Formula (all,i,TRAD_COMM)(all,r,REG)
    VALOUTPUT(i,r,"outtax") = VOM(i,r) - VOA(i,r);

Zerodivide (zero_by_zero) default 0;
Zerodivide (NONZERO_BY_ZERO) default 0;

Coefficient (all,i, TRAD_COMM)(all,r, REG)
    SUFFICIENCY(i,r) # Region's r rate of self-sufficiency for good i #;
Formula (all,i, TRAD_COMM)(all,r, REG)
    SUFFICIENCY(i,r) = VOM(i,r) / [VDM(i,r) + sum{s,REG, VIMS(i,s,r)}];

Zerodivide (zero_by_zero) off;
Zerodivide (NONZERO_BY_ZERO) off;

!<
    4. Sectors ( Comm x Reg )
    -------------------------
        NVFA
        NVPA
        NVGA

>!

Coefficient (all,i,DEMD_COMM)(all,j,PROD_COMM)(all,r,REG)(all,d,DIR)
    (all,k,PURCHVALUE)
    NVFA(i,j,r,d,k) # Firm's expenditure on i by industry j #;
Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    NVFA(i,j,r,"domestic","mktexp") = VFM(i,j,r);
Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    NVFA(i,j,r,"imported","mktexp") = 0;
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    NVFA(i,j,s,"domestic","mktexp") = VDFM(i,j,s);
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    NVFA(i,j,s,"imported","mktexp") = VIFM(i,j,s);

Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    NVFA(i,j,r,"domestic","comtax") = EVFA(i,j,r) - VFM(i,j,r);
Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    NVFA(i,j,r,"imported","comtax") = 0;
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    NVFA(i,j,s,"domestic","comtax") = VDFA(i,j,s) - VDFM(i,j,s);
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    NVFA(i,j,s,"imported","comtax") = VIFA(i,j,s) - VIFM(i,j,s);

Coefficient (all,i,TRAD_COMM)(all,s,REG)(all,d,DIR)(all,k,PURCHVALUE)
    NVPA(i,s,d,k) # Consumption expenditure on i by industry j #;
Formula (all,i,TRAD_COMM)(all,s,REG)
    NVPA(i,s,"domestic","mktexp") = VDPM(i,s);
Formula (all,i,TRAD_COMM)(all,s,REG)
    NVPA(i,s,"imported","mktexp") = VIPM(i,s);

Formula (all,i,TRAD_COMM)(all,s,REG)
    NVPA(i,s,"domestic","comtax") = VDPA(i,s) - VDPM(i,s);
Formula (all,i,TRAD_COMM)(all,s,REG)
    NVPA(i,s,"imported","comtax") = VIPA(i,s) - VIPM(i,s);

Coefficient (all,i,TRAD_COMM)(all,s,REG)(all,d,DIR)(all,k,PURCHVALUE)
    NVGA(i,s,d,k) # Government expenditure on i by industry j #;
Formula (all,i,TRAD_COMM)(all,s,REG)
    NVGA(i,s,"domestic","mktexp") = VDGM(i,s);
Formula (all,i,TRAD_COMM)(all,s,REG)
    NVGA(i,s,"imported","mktexp") = VIGM(i,s);

Formula (all,i,TRAD_COMM)(all,s,REG)
    NVGA(i,s,"domestic","comtax") = VDGA(i,s) - VDGM(i,s);
Formula (all,i,TRAD_COMM)(all,s,REG)
    NVGA(i,s,"imported","comtax") = VIGA(i,s) - VIGM(i,s);

!<
    -----------------------------------
    Write statements of the Derivatives
    -----------------------------------

    1. Macro variables ( Reg )
    --------------------------
>!
Write GDPEXP to file GTAPVIEW header "AG01" longname
    "GDPEXP = C + I + G + X - M, GDP from the expenditure side";
Write GDPSRC to file GTAPVIEW header "AG02" longname
    "GDPSRC = NETFACTINC + NETAXES + VDEP, GDP from the sources side";
Write EVFA to file GTAPVIEW header "AG03" longname
    "Sources of factor income (NETFACTINC + VDEP) by sector - EVFA(j,r)";
Write CAPITALACCT to file GTAPVIEW header "AG04" longname
    "S - I = X - M: CAPITALACCT";
Write CURRENTACCT to file GTAPVIEW header "AG05" longname
    "X - M = S - I: CURRENTACCT";
Write VKB to file GTAPVIEW header "AG06" longname
    "Capital stock by region - VKB";

!<
    2.Trade, Transport and Protection ( Reg x Reg )
    -----------------------------------------------
>!
Write VALEXPORTS to file GTAPVIEW header "BI01" longname
    "Decomposition of exports at world prices: VXWD=VXMD+XTAX";
Write VALIMPORTS to file GTAPVIEW header "BI02" longname
    "Decomposition of imports at market prices: VIMS=VIWS+MTAX";
Write CIFDECOMP to file GTAPVIEW header "BI03" longname
    "Decomposition of cif values - CIFDECOMP";

!<
    3. Commodities ( Comm x Reg )
    -----------------------------
>!
Write OUTDISP to file GTAPVIEW header "CM01" longname
    "Disposition of output - OUTDISP";
Write DOMSALESDISP to file GTAPVIEW header "CM02" longname
    "Disposition of domestic goods - DOMSALESDISP";
Write IMPSALESDISP to file GTAPVIEW header "CM03" longname
    "Disposition of imported goods - IMPSALESDISP";
Write VALOUTPUT to file GTAPVIEW header "CM04" longname
    "Value of output at market prices:  VOM = VOA + OUTTAX ";
Write SUFFICIENCY to file GTAPVIEW header "CM05" longname
    "Self-sufficiency or domestic share in total use - SUFFICIENCY";

!<
    4. Sectors ( Comm x Reg )
    -------------------------
>!

Write NVFA to file GTAPVIEW header "SF01" longname
    "Cost structure of firms - VFA";
Write NVPA to file GTAPVIEW header "SF02" longname
    "Cost structure of consumption - VPA";
Write NVGA to file GTAPVIEW header "SF03" longname
    "Cost structure of government - VGA";

!<
    --------------------------------
    Computation of Initial Tax Rates
    --------------------------------

    Calculate initial distortions as percent ad valorem rates
>!

Coefficient (all,d,TARSET)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VITQS(d,i,r,s) # Imports of i from r to s including tariff components #;
Formula  (all,d,TARSET)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VITQS(d,i,r,s) = VIWS(i,r,s); ! default value if missing !
Read (ifheaderexists) VITQS from file GTAPDATA header "VTQS";

Coefficient
    (all,i,NSAV_COMM)(all,r,REG)                   rTO(i,r);
    (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)  rTF(i,j,r);
    (all,i,TRAD_COMM)(all,r,REG)                  rTPD(i,r);
    (all,i,TRAD_COMM)(all,r,REG)                  rTPI(i,r);
    (all,i,TRAD_COMM)(all,r,REG)                  rTGD(i,r);
    (all,i,TRAD_COMM)(all,r,REG)                  rTGI(i,r);
    (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG) rTFD(i,j,r);
    (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG) rTFI(i,j,r);
    (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)       rTXS(i,r,s);
    (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)       rTMS(i,r,s);
    (all,d,TARSET)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)   rTQSMS(d,i,r,s) ;
 
Zerodivide default 1;
Formula
    (all,i,NSAV_COMM)(all,r,REG)
    rTO(i,r)    = 100*{[VOA(i,r)/VOM(i,r)]-1};

    (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    rTF(i,j,r)  = 100*{[VFA(i,j,r)/VFM(i,j,r)]-1};

    (all,i,TRAD_COMM)(all,r,REG)
    rTPD(i,r)   =  100*{[VDPA(i,r)/VDPM(i,r)]-1};

    (all,i,TRAD_COMM)(all,r,REG)
    rTPI(i,r)   = 100*{[VIPA(i,r)/VIPM(i,r)]-1};

    (all,i,TRAD_COMM)(all,r,REG)
    rTGD(i,r)   = 100*{[VDGA(i,r)/VDGM(i,r)]-1};

    (all,i,TRAD_COMM)(all,r,REG)
    rTGI(i,r)   = 100*{[VIGA(i,r)/VIGM(i,r)]-1};

    (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    rTFD(i,j,r) = 100*{[VDFA(i,j,r)/VDFM(i,j,r)]-1};

    (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    rTFI(i,j,r) = 100*{[VIFA(i,j,r)/VIFM(i,j,r)]-1};

    (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    rTXS(i,r,s) = 100*{[VXMD(i,r,s)/VXWD(i,r,s)]-1};

    (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    rTMS(i,r,s) = 100*{[VIMS(i,r,s)/VIWS(i,r,s)]-1};

    (all,d,TARSET)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    rTQSMS(d,i,r,s) = 100*{[VITQS(d,i,r,s)/VIWS(i,r,s)]-1};

   (all,d,TARSET)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG:rTQSMS(d,i,r,s)<0)
    rTQSMS(d,i,r,s) = 0;


Zerodivide off;

Write
    rTO to file TAXRATES header "rTO " longname
    "% ad valorem rate, output (or income) subsidies in region r";
    rTF to file TAXRATES header "rTF " longname
    "% ad valorem rate, taxes on primary factors";
    rTPD to file TAXRATES header "rTPD" longname
    "% ad valorem rate, private domestic consumption taxes";
    rTPI to file TAXRATES header "rTPI" longname
    "% ad valorem rate, private import consumption taxes";
    rTGD to file TAXRATES header "rTGD" longname
    "% ad valorem rate, government domestic purchases taxes";
    rTGI to file TAXRATES header "rTGI" longname
    "% ad valorem rate, government import purchases taxes";
    rTFD to file TAXRATES header "rTFD" longname
    "% ad valorem rate, taxes on firms' domestic purchases";
    rTFI to file TAXRATES header "rTFI" longname
    "% ad valorem rate, taxes on firms' imports purchases";
    rTXS to file TAXRATES header "rTXS" longname
    "% ad valorem rate, export subsidies, by destination";
    rTMS to file TAXRATES header "rTMS" longname
    "% ad valorem rate, import taxes, by source";
    rTQSMS to file TAXRATES header "rTQS" longname
    "% ad valorem rate, import taxes, by source and type";

!<
    -------------------------------
    Checking the balance conditions
    -------------------------------
>!

Coefficient (all,i,TRAD_COMM)(all,s,REG)
    CHKMKTCLIMP(i,s)
    # Checking the accounting on traded commodities supply and demand #;
Formula  (all,i,TRAD_COMM)(all,s,REG)
    CHKMKTCLIMP(i,s)
        = sum{r,REG, VIMS(i,r,s)}
        - sum{j,PROD_COMM, VIFM(i,j,s)}
        - VIPM(i,s) - VIGM(i,s);

Coefficient (all,s,REG)
    SURPLUS(s)
    # Economic surplus in region s.  This should be close to zero. #;
Formula (all,r,REG)
    SURPLUS(r)
        = sum{i,ENDW_COMM, VOA(i,r)} - VDEP(r)
        + sum{i,NSAV_COMM, VOM(i,r) - VOA(i,r)}
        + sum{j,PROD_COMM, sum{i,ENDW_COMM, VFA(i,j,r) - VFM(i,j,r)}}
        + sum{i,TRAD_COMM, VIPA(i,r) - VIPM(i,r)}
        + sum{i,TRAD_COMM, VDPA(i,r) - VDPM(i,r)}
        + sum{i,TRAD_COMM, VIGA(i,r) - VIGM(i,r)}
        + sum{i,TRAD_COMM, VDGA(i,r) - VDGM(i,r)}
        + sum{j,PROD_COMM, sum{i,TRAD_COMM, VIFA(i,j,r) - VIFM(i,j,r)}}
        + sum{j,PROD_COMM, sum{i,TRAD_COMM, VDFA(i,j,r) - VDFM(i,j,r)}}
        + sum{i,TRAD_COMM, sum{s,REG, VXWD(i,r,s) - VXMD(i,r,s)}}
        + sum{i,TRAD_COMM, sum{s,REG, VIMS(i,s,r) - VIWS(i,s,r)}}
        - sum{i,TRAD_COMM, VPA(i,r) + VGA(i,r)} - save(r);

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VTWR_M(i,r,s)
    # Value of transport services associated w/ the shipment of i from r to s #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VTWR_M(i,r,s) = VIWS(i,r,s) - VXWD(i,r,s);

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    CHKTRANS_D(i,r,s)
    # Difference between global demand and margins, should be close to zero #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    CHKTRANS_D(i,r,s) = VTWR_M(i,r,s) - sum{m, MARG_COMM, VTWR(m,i,r,s)};

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VSTT(i,r) #For check on tranport services and margines #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    VSTT(i,r) = 0;
Formula (all,i,MARG_COMM)(all,r,REG)
    VSTT(i,r) = VST(i,r);

Coefficient
    CHKTRANS_S
#Diff btwn ttl supply of trnsprt srvcs & ttl margins, should be close to zero#;
Formula
    CHKTRANS_S
        = sum{m,MARG_COMM,sum{i,TRAD_COMM,sum{r,REG,sum{s,REG, VTWR(m,i,r,s)}}}}
        - sum{i,TRAD_COMM,sum{r,REG, VSTT(i,r)}};

!<
    ------------------------------------------
    Calculating Summary Measures for Imbalance
    ------------------------------------------

    For each GTAPDATA calculate a summary measure of imbalance, the sum of
    the absolute magnitudes of the imbalances in components.  Calculate an
    overall summary measure, the sum of the variable-specific summary
    measures.
>!

Coefficient
    SUMTRANS_S
    # Summary measure for transport services, should be close to zero #;
Formula
    SUMTRANS_S = abs(CHKTRANS_S);

Coefficient
    SUMTRANS_D
    # Summary measure for transport services, should be close to zero #;
Formula
    SUMTRANS_D
        = sum{i, TRAD_COMM, sum{r, REG, sum{s, REG, abs(CHKTRANS_D(i,r,s))}}};

Coefficient
    SUMSURPLUS
    # Summary measure for economic surplus, should be close to zero #;
Formula
    SUMSURPLUS = sum{r, REG, abs(SURPLUS(r))};

Coefficient
    SUMMKTCLIMP
    # Summary measure for excess importation, should be close to zero #;
Formula
    SUMMKTCLIMP = sum{i,TRAD_COMM, sum{s,REG, abs(CHKMKTCLIMP(i,s))}};

Coefficient
    SUMIMBALS # Summary measure of imbalances, should be close to zero #;
Formula
    SUMIMBALS
        = SUMTRANS_S + SUMTRANS_D + SUMSURPLUS + SUMMKTCLIMP;

!<
    -------------------------------------
    Writing Summary Measures of Imbalance
    -------------------------------------
>!

Write SUMIMBALS   to file GTAPVIEW header "0IMB";
Write SUMTRANS_S  to file GTAPVIEW header "1TNS";
Write SUMTRANS_D  to file GTAPVIEW header "1TND";
Write SUMSURPLUS  to file GTAPVIEW header "1SRP";
Write SUMMKTCLIMP to file GTAPVIEW header "1IMP";
Write CHKTRANS_S  to file GTAPVIEW header "2TNS";
Write CHKTRANS_D  to file GTAPVIEW header "2TND";
Write SURPLUS     to file GTAPVIEW header "2SRP";
Write CHKMKTCLIMP to file GTAPVIEW header "2IMP";

!<
    ----------------------------
    Checking the sign conditions
    ----------------------------
>!

Coefficient ! work matrices for holding binary flags: re-used to save RAM !
(all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG) SIGNEPR(i,j,r);
(all,i,ENDW_COMM)(all,r,REG) SIGNER(i,r);
(all,k,MARG_COMM)(all,r,REG) SIGNMR(k,r);
(all,k,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG) SIGNMTRR(k,i,r,s);
(all,r,REG) SIGNR(r);
(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG) SIGNTPR(i,j,r);
(all,i,TRAD_COMM)(all,r,REG) SIGNTR(i,r);
(all,i,TRAD_COMM)(all,r,REG)(all,s,REG) SIGNTRR(i,r,s);

Formula (all,i,ENDW_COMM)(all,r,REG)
      SIGNER(i,r) = IF(EVOA(i,r)<0, 1);
Write SIGNER to file GTAPVIEW header "SVOA"
 longname "EVOA bad-sign flag; 1 if negative, else 0";

Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
      SIGNEPR(i,j,r) = IF(EVFA(i,j,r)<0, 1);
Write SIGNEPR to file GTAPVIEW header "SVFA"
 longname "EVFA bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
      SIGNTPR(i,j,r) = IF(VIFA(i,j,r)<0, 1);
Write SIGNTPR to file GTAPVIEW header "SIFA"
 longname "VIFA bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
      SIGNTPR(i,j,r) = IF(VDFA(i,j,r)<0, 1);
Write SIGNTPR to file GTAPVIEW header "SDFA"
 longname "VDFA bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,r,REG)
      SIGNTR(i,r) = IF(VIPA(i,r)<0, 1);
Write SIGNTR to file GTAPVIEW header "SIPA"
 longname "VIPA bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,r,REG)
      SIGNTR(i,r) = IF(VDPA(i,r)<0, 1);
Write SIGNTR to file GTAPVIEW header "SDPA"
 longname "VDPA bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,r,REG)
      SIGNTR(i,r) = IF(VIGA(i,r)<0, 1);
Write SIGNTR to file GTAPVIEW header "SIGA"
 longname "VIGA bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,r,REG)
      SIGNTR(i,r) = IF(VDGA(i,r)<0, 1);
Write SIGNTR to file GTAPVIEW header "SDGA"
 longname "VDGA bad-sign flag; 1 if negative, else 0";

Formula (all,r,REG)
      SIGNR(r) = IF(VKB(r)<0, 1);
Write SIGNR to file GTAPVIEW header "SKB"
 longname "VKB bad-sign flag; 1 if negative, else 0";

Formula (all,r,REG)
      SIGNR(r) = IF(VDEP(r)<0, 1);
Write SIGNR to file GTAPVIEW header "SDEP"
 longname "VDEP bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
      SIGNTRR(i,r,s) = IF(VXMD(i,r,s)<0, 1);
Write SIGNTRR to file GTAPVIEW header "SXMD"
 longname "VXMD bad-sign flag; 1 if negative, else 0";

Formula (all,i,MARG_COMM)(all,r,REG)
      SIGNMR(i,r) = IF(VST(i,r)<0, 1);
Write SIGNMR to file GTAPVIEW header "SST"
 longname "VST bad-sign flag; 1 if negative, else 0";

Formula (all,k,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
      SIGNMTRR(k,i,r,s) = IF(VTWR(k,i,r,s)<0, 1);
Write SIGNMTRR to file GTAPVIEW header "STWR"
 longname "VTWR bad-sign flag; 1 if negative, else 0";

Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
      SIGNEPR(i,j,r) = IF(VFM(i,j,r)<0, 1);
Write SIGNEPR to file GTAPVIEW header "SFM"
 longname "VFM bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
      SIGNTPR(i,j,r) = IF(VIFM(i,j,r)<0, 1);
Write SIGNTPR to file GTAPVIEW header "SIFM"
 longname "VIFM bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
      SIGNTPR(i,j,r) = IF(VDFM(i,j,r)<0, 1);
Write SIGNTPR to file GTAPVIEW header "SDFM"
 longname "VDFM bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,r,REG)
      SIGNTR(i,r) = IF(VIPM(i,r)<0, 1);
Write SIGNTR to file GTAPVIEW header "SIPM"
 longname "VIPM bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,r,REG)
      SIGNTR(i,r) = IF(VDPM(i,r)<0, 1);
Write SIGNTR to file GTAPVIEW header "SDPM"
 longname "VDPM bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,r,REG)
      SIGNTR(i,r) = IF(VIGM(i,r)<0, 1);
Write SIGNTR to file GTAPVIEW header "SIGM"
 longname "VIGM bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,r,REG)
      SIGNTR(i,r) = IF(VDGM(i,r)<0, 1);
Write SIGNTR to file GTAPVIEW header "SDGM"
 longname "VDGM bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
      SIGNTRR(i,r,s) = IF(VIMS(i,r,s)<0, 1);
Write SIGNTRR to file GTAPVIEW header "SIMS"
 longname "VIMS bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
      SIGNTRR(i,r,s) = IF(VXWD(i,r,s)<0, 1);
Write SIGNTRR to file GTAPVIEW header "SXWD"
 longname "VXWD bad-sign flag; 1 if negative, else 0";

Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
      SIGNTRR(i,r,s) = IF(VIWS(i,r,s)<0, 1);
Write SIGNTRR to file GTAPVIEW header "SIWS"
 longname "VIWS bad-sign flag; 1 if negative, else 0";



! addition jmh Mar 2008 !
Set PRODTAX (PRODTAX);
Set MORECOLS (hou,gov,exp,lessDomTax,lessImpTax,lessTariff,lessImpCIF);
Set SUMCOLS = PROD_COMM + MORECOLS;
Set XXX = TRAD_COMM + ENDW_COMM;
Set SUMROWS = XXX + PRODTAX;
Subset DEMD_COMM is subset of XXX;

Coefficient
(all,q,SUMROWS)(all,c,SUMCOLS)(all,r,REG) SUMMARY(q,c,r)
 # Summary producer price flows table #;
Formula
(all,i,SUMROWS)(all,j,SUMCOLS)(all,r,REG) SUMMARY(i,j,r) = 0;
(all,i,DEMD_COMM)(all,j,PROD_COMM)(all,r,REG) SUMMARY(i,j,r) = VFA(i,j,r);
(all,i,PROD_COMM)(all,r,REG) SUMMARY("ProdTax",i,r) = VOM(i,r) - VOA(i,r);
(all,i,TRAD_COMM)(all,r,REG) SUMMARY(i,"gov",r) = VDGA(i,r) + VIGA(i,r);
(all,i,TRAD_COMM)(all,r,REG) SUMMARY(i,"hou",r) = VPA(i,r);
(all,i,TRAD_COMM)(all,r,REG) SUMMARY(i,"exp",r)
              = sum{s,REG, VXWD(i,r,s)} + VSTT(i,r);

(all,i,TRAD_COMM)(all,r,REG) SUMMARY(i,"lessDomTax",r) =
-  [VDPA(i,r) - VDPM(i,r)] ! dom hou !
-  [VDGA(i,r) - VDGM(i,r)] ! dom gov !
-  [sum{s,REG,        VXWD(i,r,s) - VXMD(i,r,s)}] ! exp tax !
-  [sum{j,PROD_COMM,  VDFA(i,j,r) - VDFM(i,j,r)}]; ! dom ind+inv !

(all,i,TRAD_COMM)(all,r,REG) SUMMARY(i,"lessImpTax",r) =
-  [VIPA(i,r) - VIPM(i,r)] ! imp hou !
-  [VIGA(i,r) - VIGM(i,r)] ! imp gov !
-  [sum{j,PROD_COMM,  VIFA(i,j,r) - VIFM(i,j,r)}]; ! imp ind+inv !

(all,i,TRAD_COMM)(all,r,REG) SUMMARY(i,"lessTariff",r) =
  - sum{s,REG, VIMS(i,s,r) - VIWS(i,s,r)};

(all,i,TRAD_COMM)(all,r,REG) SUMMARY(i,"lessImpCIF",r) =
-  VIPM(i,r)  ! imp hou !
-  VIGM(i,r)  ! imp gov !
-  sum{j,PROD_COMM,  VIFM(i,j,r)} - SUMMARY(i,"lessTariff",r); ! imp ind+inv !

Write SUMMARY to file GTAPVIEW header "SMRY";

Coefficient
(all,s,TRAD_COMM)(all,r,REG) SUMBAL(s,r) # Summary balance -- should be tiny #;
Formula (all,s,TRAD_COMM)(all,r,REG) SUMBAL(s,r) =
         sum{q,SUMROWS, SUMMARY(q,s,r)} - sum{j,SUMCOLS, SUMMARY(s,j,r)};
Write SUMBAL to file GTAPVIEW header "SBAL";

! why are Bulgarian IO multipliers so high ? !
Set SUMREP (IntCosts,FacCosts,ImpCIF,Exp,GDP);
Coefficient (all,r,REG)(all,s,SUMREP) MACSUM(r,s) # another macro summary #;
Formula (all,r,REG)(all,s,SUMREP)     MACSUM(r,s) = 0.0;
 (all,r,REG) MACSUM(r,"IntCosts") =
                            sum{i,TRAD_COMM,sum{k,TRAD_COMM,SUMMARY(i,k,r)}};
 (all,r,REG) MACSUM(r,"FacCosts") = sum{i,ENDW_COMM, VOA(i,r)};
 (all,r,REG) MACSUM(r,"ImpCIF") = -sum{i,TRAD_COMM,SUMMARY(i,"lessImpCIF",r)};
 (all,r,REG) MACSUM(r,"Exp") = sum{i,TRAD_COMM,SUMMARY(i,"exp",r)};
 (all,r,REG) MACSUM(r,"GDP") = sum{k,GDPEXPEND, GDPEXP(r,k)};
Write MACSUM to file GTAPVIEW header "MSUM";

! For use with GTAPAGG !
Transfer (ifheaderexists) "LICN" from file GTAPSETS to file GTAPVIEW ;
Transfer (ifheaderexists) "H1L" from file GTAPSETS to file GTAPVIEW ;
Transfer (ifheaderexists) "H2L" from file GTAPSETS to file GTAPVIEW ;




!<
    ------------------------
    End of GTAPVIEW.TAB file
    ------------------------
>!

